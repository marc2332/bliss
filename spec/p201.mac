def p201_config(cnum, type, unit, module, chan) '{
   local mne factor ctrl_dev

   global P201_CT[]

   ctrl_dev = p201_ADDR
   if (type == "ctrl") {
      printf("Using P201 \"%s\" counters\n", ctrl_dev)
      return
   }

   mne = cnt_mne(cnum)
   if (chan == 0) {
      printf("Configuring \"%s\" as timer\n", mne)
      factor = counter_par(cnum, "scale")
      tango_put(ctrl_dev, "timer_freq", factor)
      tango_put(ctrl_dev, "acq_mode", "Internal")
      P201_CT[ctrl_dev]["int_cnt"] = 11
   }
}'

def p201_cmd(cnum, key, p1, p2) '{
   local chan is_running ctrl_dev acq_chan acq_chan_list[] sep val[] int_cnt

   ctrl_dev = p201_ADDR
   if (cnum != "..")
      chan = counter_par(cnum, "channel")

   if (key == "get_status") {
      is_running = (tango_get(ctrl_dev, "acq_status") == "Running")
      P201_CT[ctrl_dev]["running"] = is_running
      return is_running
   } else if (key == "prestart_all") {
      P201_CT[ctrl_dev]["acq_channels"] = ""
   } else if (key == "start_one") {
      if (chan != 0) {
          acq_chan = P201_CT[ctrl_dev]["acq_channels"]
	  sep = length(acq_chan) ? " " : ""
	  acq_chan = sprintf("%s%s%d", acq_chan, sep, chan)
	  P201_CT[ctrl_dev]["acq_channels"] = acq_chan
      } else {
          tango_put(ctrl_dev, "acq_expo_time", p1)
          split(P201_CT[ctrl_dev]["acq_channels"], acq_chan_list)
          tango_put(ctrl_dev, "acq_channels", acq_chan_list)
          tango_put(ctrl_dev, "acq_nb_points", 1)
          tango_io(ctrl_dev, "prepare_acq")
          tango_io(ctrl_dev, "start_acq")
      }
   } else if (key == "counts") {
      int_cnt = P201_CT[ctrl_dev]["int_cnt"]
      tango_get(ctrl_dev, "latches", val)
      if (val[int_cnt - 1] == 0)
            tango_get(ctrl_dev, "counters", val)
      if (val[int_cnt - 1] == 0)
            tango_get(ctrl_dev, "latches", val)
      chan = chan ? chan : int_cnt
      return val[chan - 1]
   } else if (key == "halt_all") {
      tango_io(ctrl_dev, "stop_acq")
   }
}'

def p201_par(cnum, key, action, val) '{
   local ctrl_dev mode_str acq_mode acq_mode_table[]

   acq_mode_table[0] = "Internal"
   acq_mode_table[1] = "Slave"
   acq_mode_table["nr"] = 2

   ctrl_dev = p201_ADDR
   if (key == "acq_mode") {
       mode_str = tango_get(ctrl_dev, "acq_mode")
       for (acq_mode = 0; acq_mode < acq_mode_table["nr"]; acq_mode++)
           if (acq_mode_table[acq_mode] == mode_str)
               break
       if (action == "set")
           tango_put(ctrl_dev, "acq_mode", acq_mode_table[val])
       return acq_mode
   } else if (key == "trigger_point") {
       tango_io(ctrl_dev, "trigger_point")
       return 0
   }
}'
